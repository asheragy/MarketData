plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'androidx.navigation.safeargs.kotlin' version "$androidxNavigationVersion"
    id 'org.jetbrains.kotlin.plugin.compose'
}



def apikeyPropertiesFile = rootProject.file("stockcharts/apikey.properties")
def apikeyProperties = new Properties()
if(apikeyPropertiesFile.exists())
    apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    namespace 'org.cerion.stockcharts'
    compileSdk 35

    defaultConfig {
        applicationId "org.cerion.stockcharts"
        minSdkVersion 28
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "TD_CONSUMER_KEY", apikeyProperties.get('td_consumer_key', "\"\""))
        buildConfigField("String", "TD_REDIRECT_URI", apikeyProperties.get('td_redirect_uri', "\"\""))
        buildConfigField("String", "TIINGO_APIKEY", apikeyProperties.get('tiingo', "\"\""))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        compose true
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.2'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.9.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidxNavigationVersion"
    // TODO can be removed if live data is
    implementation 'androidx.compose.runtime:runtime-livedata:1.9.0'

    //implementation 'com.google.android.material:material:1.12.0'

    def coroutines_version = "1.10.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    def room_version = "2.7.2"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2025.08.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    // TODO remove after removing livedata usages
    //implementation 'androidx.compose.runtime:runtime-livedata:1.9.0'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    //androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    //debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // 3rd party
    implementation "io.insert-koin:koin-android:4.1.0"
    androidTestImplementation "io.insert-koin:koin-test:4.1.0"
    implementation 'com.github.AppDevNext:AndroidChart:3.1.0.30'
    implementation project(':core')
    implementation project(':webclients')

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "androidx.test:core:1.7.0"

    // Android Test
    androidTestImplementation "androidx.test.ext:junit:1.3.0"
    debugImplementation "androidx.fragment:fragment-testing:1.8.9"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.7.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.7.0"
}

repositories {
    google()
    maven { url 'https://jitpack.io' }
}

tasks.register('createIfNotExists') {
    doLast {
        def targetFile = file('src/main/res/raw/crypto.json')
        if (!targetFile.exists()) {
            targetFile.parentFile.mkdirs() // ensure parent directories exist
            targetFile.text = '{}'
            println "Created ${targetFile}"
        } else {
            println "${targetFile} already exists"
        }
    }
}

tasks.named('preBuild') {
    dependsOn 'createIfNotExists'
}